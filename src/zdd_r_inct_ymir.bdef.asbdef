    managed implementation in class zbp_dd_r_inct_ymir unique;
    strict ( 2 );
    with draft;
    define behavior for ZDD_R_INCT_YMIR alias Incident


    persistent table zdt_inct_ymir
    draft table zdtinct_ymir_d


    etag master LocalLastChangedAt
    lock master total etag LastChangedAt
    authorization master ( global, instance )


    {

      //ASIGNAR EL UUID DE LA CLAVE PRINCIPAL SI NO LO ENVÍA EL UI
      field ( numbering : managed, readonly ) IncUUID;


    //CAMPOS DE SOLO LECTURA PARA CONTROL DE VERSIONES Y AUTORÍA
      field ( readonly )
      LocalCreatedBy,
      LocalCreatedAt,
      LocalLastChangedBy,
      LocalLastChangedAt,
      LastChangedAt;


     //VALIDACIONES DE LOS CAMPOS EN CREATE Y UPDATE
      validation validateFields on save { create; update; field Title, Description, Priority, CreationDate, ChangedDate; }


      //CAMPOS MANDATORIOS Y DE SOLO LECTURA
      field ( mandatory   ) Title, Description, Priority;
      field ( readonly ) IncidentId, Status;

      //CAMPOS DE SOLO LECTURA QUE SE ASIGNAN AUTOM
      field ( features : instance ) CreationDate, ChangedDate;

      create;

      update;

      delete;

    //ASOCIACIÓN PARA HISTÓRICO DE CAMBIOS DE ESTATUS
      association _History { create; with draft; }

    //SE AFECTUA CUANDO SE EJECUTA EL CAMBIO DE ESTADO
      side effects
      {
        action changeStatus affects entity _History;
      }

      //ACCION DE CAMBIAR ESTADO
      action ( features : instance, authorization : update ) changeStatus
        parameter ZDD_AE_CHANGE_STATUS_YMIR
        result [1] $self;


    //DETERMINACION PARA ASIGNAR VALORES INICIALES EN CREATE Y PARA CREAR EL HISTÓRICO DE CAMBIOS DE ESTATUS
      determination setInitialValues on modify { create; }
      determination createInitialHistory on save { create; }



      draft action Activate optimized;

      draft action Discard;

      draft action Edit;

      draft action Resume;


      //SE EJECUTA LA VALIDACIÓN DE CAMPOS EN SAVE CUANDO SE CREA O SE ACTUALIZA UN INCIDENTE
      draft determine action Prepare
      {
        validation validateFields;
      }


    //MAPEO DEL DRAFT DE LA TABLA PRINCIPAL
      mapping for zdt_inct_ymir corresponding
        {
          IncUUID            = inc_uuid;
          IncidentID         = incident_id;
          Title              = title;
          Description        = description;
          Status             = status;
          Priority           = priority;
          CreationDate       = creation_date;
          ChangedDate        = changed_date;
          LocalCreatedBy     = local_created_by;
          LocalCreatedAt     = local_created_at;
          LocalLastChangedBy = local_last_changed_by;
          LocalLastChangedAt = local_last_changed_at;
          LastChangedAt      = last_changed_at;
        }

    }

// Comportamiento para el histórico de cambios de estatus del incidente
    define behavior for ZDD_INCT_H_YMIR alias History
    persistent table zdt_inct_h_ymir
    draft table zdtinct_h_ymir_d
    lock dependent by _Incident
    authorization dependent by _Incident
    etag master LocalLastChangedAt
    {
      update;

      delete;
      mapping for zdt_inct_h_ymir
        {
          HisUUID            = his_uuid;
          IncUUID            = inc_uuid;
          HisID              = his_id;
          PreviousStatus     = previous_status;
          NewStatus          = new_status;
          Text               = text;
          LocalCreatedBy     = local_created_by;
          LocalCreatedAt     = local_created_at;
          LocalLastChangedBy = local_last_changed_by;
          LocalLastChangedAt = local_last_changed_at;
          LastChangedAt      = last_changed_at;
        }



      // Asociación para regresar al padre

      association _Incident { with draft; }



      // Determinar automáticamente el UUID de la clave si no lo envía el UI

      field ( readonly, numbering : managed ) HisUUID;
      field ( readonly ) IncUUID, HisId, NewStatus, PreviousStatus, Text;

    }